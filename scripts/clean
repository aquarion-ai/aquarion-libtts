#!/usr/bin/env nu
# SPDX-FileCopyrightText: 2025-present Krys Lawrence <aquarion.5.krystopher@spamgourmet.org>
# SPDX-License-Identifier: AGPL-3.0-only

# Part of the aquarion-libtts library of the Aquarion AI project.
# Copyright (C) 2025-present Krys Lawrence <aquarion.5.krystopher@spamgourmet.org>
#
# This program is free software: you can redistribute it and/or modify it under the
# terms of the GNU Affero General Public License as published by the Free Software
# Foundation, version 3.
#
# This program is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
# PARTICULAR PURPOSE. See the GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License along with
# this program. If not, see <https://www.gnu.org/licenses/>.


# Clean up this development environment.
#
# By default it just cleans up generated artifacts like build distributions, generated
# docs, etc.
#
# To restore the environment, run the "init" script.
def main [
    --envs (-e)  # Also clean virtual environments
    --full (-f)  # Also clean all local data, caches, configs & pre-commit hooks
    --devbox (-d)  # Also remove the devbox shell
] : nothing -> nothing {
    remove_local_builds
    remove_generated_translation_files
    remove_example_outputs
    remove_generated_documentation
    if $envs or $full {
        remove_hatch_envs
    }
    if $full {
        # Note: The order of operations matters here.
        remove_hatch_pythons
        remove_pre_commit_hooks
        remove_uv_tools
        remove_uv_pythons
        remove_local
    }
    if $devbox {
        remove_devbox_shell
        print "If you also want to clean the devbox (NIX) cache, exit the devbox shell and run:"
        print "  nix-collect-garbage -d"
    }
}

## Internal commands

def remove_hatch_envs []: nothing -> nothing {
    let envs = (
        hatch env show --json --internal
        | from json
        | transpose name record
        | get name
    )
    for environ in ($envs) {
        print $"Removing hatch environment: ($environ)"
        hatch env remove $environ
    }
}

def remove_local_builds []: nothing -> nothing {
    print "Removing local builds"
    rm -rf dist
    rm -rf docs/site
    for dir in (glob **/__pycache__) {
        rm -rf $dir
    }
}

def remove_generated_translation_files []: nothing -> nothing {
    let files = (ls ...(glob **/*.{mo,pot}) | each {$in.name})
    for file in ($files) {
        print $"Removing generated translation file: ($file)"
        rm $file
    }
}

def remove_example_outputs []: nothing -> nothing {
    let files = (ls ...(glob examples/*.wav) | each {$in.name})
    for file in ($files) {
        print $"Removing example output: ($file)"
        rm $file
    }
}

def remove_generated_documentation []: nothing -> nothing {
    print "Removing generated documentation."
    rm -rf docs/site
}

def remove_hatch_pythons []: nothing -> nothing {
    print (hatch python remove all)
}

def remove_uv_tools []: nothing -> nothing {
    print (uv tool uninstall --all)
}

def remove_uv_pythons []: nothing -> nothing {
    print (uv python uninstall --all)
}

def remove_pre_commit_hooks []: nothing -> nothing {
    pre-commit uninstall
    pre-commit clean
    pre-commit gc
}

def remove_local []: nothing -> nothing {
    print "Removing local data, caches & configs"
    rm -rf .local
}

def remove_devbox_shell []: nothing -> nothing {
    print "Removing devbox shell"
    rm -rf .devbox
    print $"(ansi red)ATTENTION: YOU MUST EXIT THE DEVBOX SHELL IMMEDIATELY AFTER THIS COMMAND!(ansi reset)"
}
